buildscript {
    dependencies {
        classpath dep.graphqlSupport
    }
}

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kolibree-publisher'
}

def VERSION_NAME="14.1.0-kl"

android {
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    compileSdkVersion androidConfig.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += '../libs/graphql_java_gen/support/src/main/java'
    }

    defaultConfig {
        minSdkVersion androidConfig.minSdkVersion

        buildConfigField "String", "BUY_SDK_VERSION", "\"$VERSION_NAME\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            testCoverageEnabled project.hasProperty('withCoverage')
        }

        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

archivesBaseName = POM_ARTIFACT_ID
version = VERSION_NAME

dependencies {
    implementation dep.okhttp
    implementation dep.androidSupportAnnotations
    implementation dep.jodaTime
    implementation dep.gson
    implementation dep.kotlinStdLib
    implementation dep.timber
    implementation dep.graphqlSupport

    testImplementation dep.mockitoCore
    testImplementation dep.mockitokotlin
    testImplementation dep.junit
    testImplementation dep.truth
    testImplementation dep.mockWebServer
}


File localProperties = project.rootProject.file('local.properties')
Properties properties = new Properties()
if (localProperties.exists()) {
    properties.load(localProperties.newDataInputStream())
}

tasks.withType(Checkstyle) {
    exclude '**/Storefront.java'
}

group "com.shopify.mobilebuysdk"

publisher {
    groupId = "com.shopify.mobilebuysdk"
    artifactId = "buy3"
    version = VERSION_NAME

    repositoryUrl = kolibree_artifactory_url
    repositoryUsername = kolibree_artifactory_username
    repositoryPassword = kolibree_artifactory_password
}
